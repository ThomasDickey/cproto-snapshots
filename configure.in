dnl Process this file with 'autoconf' to produce a 'configure' script
dnl $Id: configure.in,v 4.17 2015/07/06 00:41:22 tom Exp $
AC_REVISION($Revision: 4.17 $)
AC_PREREQ(2.52.20030208)
AC_INIT(cproto.c)
AC_CONFIG_HEADER(config.h:config_h.in)
CF_CHECK_CACHE

CF_PROG_CC
AC_PROG_CPP
AC_PROG_LEX
AC_PROG_MAKE_SET
AC_PROG_YACC
AC_PROG_INSTALL
CF_MAKE_TAGS
CF_PROG_LINT

CF_XOPEN_SOURCE

CF_WITH_WARNINGS
CF_DISABLE_ECHO

if test -n "$LINT" ; then
	cf_have_lint=yes
else
	cf_have_lint=no
fi

AC_ARG_ENABLE(llib,
[  --enable-llib           enable lint-library (llib) support],
[cf_enable_llib=$enableval],
[cf_enable_llib=$cf_have_lint])

if test "$cf_enable_llib" = yes ; then
	AC_DEFINE(OPT_LINTLIBRARY)
fi

###	extra things that we'll substitute in the makefile
dnl AC_SUBST(LIBS)
AC_SUBST(EXTRA_OBJS)

AC_CONST
AC_STDC_HEADERS
AC_HAVE_HEADERS(stdlib.h unistd.h)
AC_HAVE_HEADERS(getopt.h string.h memory.h)

AC_HAVE_FUNCS(getopt popen strstr tmpfile link unlink)

if test "x$ac_cv_func_getopt" = xno
then
	CPPFLAGS="$CPPFLAGS -I\$(srcdir)/porting"
	EXTRA_OBJS="$EXTRA_OBJS getopt.o"
fi

if test "x$ac_cv_func_popen" = xno
then
	CPPFLAGS="$CPPFLAGS -I\$(srcdir)/porting"
	EXTRA_OBJS="$EXTRA_OBJS popen.o"
fi

###	special configuration tests
CF_MKSTEMP
CF_SIZE_T
CF_YACC_ERROR
CF_POPEN_TEST

###	debugging/testing
AC_ARG_WITH(trace,
[  --with-trace            use trace.c for memory-testing],[
	AC_HEADER_TIME
	AC_HAVE_FUNCS(gettimeofday)
	AC_DEFINE(DOALLOC,10000)
	EXTRA_OBJS="$EXTRA_OBJS trace.o dump.o"])
CF_DISABLE_LEAKS
CF_WITH_MAN2HTML

echo setting other definitions
AC_DEFINE_UNQUOTED(CPP,"$CPP")

###	output makefile and config.h
AC_OUTPUT(Makefile,,,cat)
CF_MAKE_DOCS(cproto,1)
